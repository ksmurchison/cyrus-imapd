#!perl
use Cassandane::Tiny;

sub test_addressbook_set_isdefault
    ($self)
{
    my $user = $self->default_user;
    my $jmap = $user->jmap; 

    xlog $self, "get default addressbook";
    my $res = $jmap->request([[
        'AddressBook/get' => {
        },
    ]])->single_sentence("AddressBook/get")->arguments;

    $self->assert_cmp_deeply(
        superhashof({
            isDefault => JSON::true,
            myRights => superhashof({
                mayDelete => JSON::false
            })
        }),
        $res->{list}[0]
    );
    my $id1 = $res->{list}[0]{id};

    xlog $self, "create addressbook";
    $res = $jmap->request([[
        'AddressBook/set', {
            create => {
                1 => {
                    name => "foo",
                    description => "My foo",
                }
            }
        },
    ]])->single_sentence("AddressBook/set")->arguments;

    my $id2 = $res->{created}{1}{id};
    my $state = $res->{newState};

    xlog $self, "change default addressbook";
    $res = $jmap->request([[
        'AddressBook/set', {
            onSuccessSetIsDefault => $id2
        },
    ]])->single_sentence("AddressBook/set")->arguments;

    $self->assert_cmp_deeply(
        superhashof({
            $id1 => {isDefault => JSON::false},
            $id2 => {isDefault => JSON::true}
        }),
        $res->{updated}
    );

    $res = $jmap->request([[
        'AddressBook/changes' => {
            sinceState => $state
        },
    ]])->single_sentence("AddressBook/changes")->arguments;

    $self->assert_cmp_deeply(
        bag($id1, $id2),
        $res->{updated}
    );

    xlog $self, "get addressbooks";
    $res = $jmap->request([[
        'AddressBook/get' => {
        },
    ]])->single_sentence("AddressBook/get")->arguments;

    $self->assert_cmp_deeply(
        superbagof(
            superhashof({
                id => $id1,
                isDefault => JSON::false,
                myRights => superhashof({
                    mayDelete => JSON::true
                })
            }),
            superhashof({
                id => $id2,
                isDefault => JSON::true,
                myRights => superhashof({
                    mayDelete => JSON::false
                })
            }),
        ),
        $res->{list}
    );

    xlog $self, "delete the original default create another addressbook and make it the default";
    $res = $jmap->request([[
        'AddressBook/set', {
            destroy => [ $id1 ],
            create => {
                2 => {
                    name => "bar",
                    description => "My bar",
                }
            },
            onSuccessSetIsDefault => '#2'
        },
    ]])->single_sentence("AddressBook/set")->arguments;

    $self->assert_cmp_deeply(
        superhashof({
            created => {
                2 => superhashof({
                    isDefault => JSON::true
                })
            },
            updated => {                   
                $id2 => {isDefault => JSON::false},
            },
            destroyed => [ $id1 ]
        }),
        $res
    );
    my $id3 = $res->{created}{2}{id};

    xlog $self, "try to deleted the current default";
    $res = $jmap->request([[
        'AddressBook/set', {
            destroy => [ $id3 ],
        },
    ]])->single_sentence("AddressBook/set")->arguments;

    $self->assert_cmp_deeply(
        superhashof({
            notDestroyed => {
                $id3 => { type => 'forbidden' }
            },
        }),
        $res
    );

    xlog $self, "try to change the default along with an update that will fail";
    $res = $jmap->request([[
        'AddressBook/set', {
            update => {
                foo => {
                    name => "baz",
                }
            },
            onSuccessSetIsDefault => $id3
        }
    ]])->single_sentence("AddressBook/set")->arguments;

    $self->assert_cmp_deeply(
        superhashof({
            notUpdated => {
                foo => { type => 'notFound' }
            },
            updated => JSON::null
        }),
        $res
    );

    xlog $self, "try to change the default to a bogus addressbook id";
    $res = $jmap->request([[
        'AddressBook/set', {
            onSuccessSetIsDefault => 'foo'
        }
    ]])->single_sentence("AddressBook/set")->arguments;

    $self->assert_equals(JSON::null, $res->{updated});
}

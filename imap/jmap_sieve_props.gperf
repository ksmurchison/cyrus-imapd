%{
/* jmap_sieve_props.c -- Lookup function for JMAP Sieve properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

#include "jmap_api.h"

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      SIEVE_PROP_
%define word-array-name       sieve_prop_array
%define length-table-name     sieve_prop_lengths
%define hash-function-name    sieve_prop_hash
%define lookup-function-name  sieve_prop_lookup

jmap_property_t;

%%
"id",       NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"name",     NULL, 0
"isActive", NULL, JMAP_PROP_SERVER_SET
"blobId",   NULL, 0
%%

const jmap_prop_hash_table_t jmap_sieve_props_map = {
    sieve_prop_array,
    SIEVE_PROP_TOTAL_KEYWORDS,
    SIEVE_PROP_MIN_HASH_VALUE,
    SIEVE_PROP_MAX_HASH_VALUE,
    &sieve_prop_lookup
};
